# A soundwave object. Largely customizable.
#  Intended to be spawned via noisy objects.
# All times are in seconds.

{
id: "sound_wave",
zorder: "@include data/zorder.cfg:soundwave",
is_strict: true,
always_active: true,
editor_info: { category: "effects" },

properties: {
	imgDiameter: "int :: 1024", // Set this to the image size
	imgRadius:   "imgDiameter/2",

	initRadius:  { type: 'int', default: 5 },   // Pixels upon spawning
	carryDist: { type: 'int', default: 400 }, // Pixels until dieing
	
	lifetime:   { type: 'int', default: 120 },  // Cycles until dieing
	timeToLive: "lifetime - me.cycle",          // Cycles left

	// Scale grown per cycle
	// We divide by imgRadius to convert from pixels to image scale
	growthSpeed: "decimal :: 
		(decimal(carryDist - initRadius)/lifetime)/imgRadius",


	rotateSpeed: "decimal :: 5.0", // Degrees per cycle
	initAlpha:   "int :: 200",
	rgb:         { type: '[int]', default: [255,255,255] },
},

on_create: "[
	// We don't want to show up when the player can see.
//	if(not level.player.is_blind, die()),

	set(scale, initRadius / imgRadius),
	set(red,   rgb[0]),
	set(green, rgb[1]),
	set(blue,  rgb[2]),
]",
	
on_process: "[
	add(scale,  growthSpeed),
	add(rotate, rotateSpeed), // By rotating, it's harder to see the pixels
	set(alpha,  int(initAlpha*timeToLive/decimal(lifetime))),

	if(timeToLive < 0, die())
]",

animation: [
	{
		id: "normal",
		image: "circle.png",
		rect: [0,0,511,511],
		frames: 1
	}
]
}
